import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    ext {
        springBootVersion = "2.1.8.RELEASE"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}


plugins {
    id "java"
    id "org.flywaydb.flyway" version "5.2.4"
}

apply plugin: 'org.springframework.boot'

repositories {
    mavenCentral()
}

dependencies {
    compile("org.hibernate.validator:hibernate-validator:6.0.17.Final")
    compile("org.springframework:spring-web:5.1.9.RELEASE")
    compile("org.springframework:spring-webmvc:5.1.9.RELEASE")
    compile("org.springframework.boot:spring-boot-starter:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-json:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-tomcat:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion")
    compile("mysql:mysql-connector-java:6.0.6")
    testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
    testCompile("org.mockito:mockito-core:2.23.4")
    compile("org.springframework.boot:spring-boot-starter-actuator:$springBootVersion")


}
def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"

def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"

flyway {
    url = developmentDbUrl
    user = "tracker"
    password = ""
    locations = ["filesystem:databases/tracker/migrations"]
}

task testMigrate(type: FlywayMigrateTask) {
    url = testDbUrl
}

bootRun.environment([
        "SPRING_DATASOURCE_URL": developmentDbUrl,
        "WELCOME_MESSAGE": "hello",
        "MANAGEMENT_ENDPOINTS_WEB_EXPOSURE_INCLUDE": "*",
        "MANAGEMENT_ENDPOINT_HEALTH_SHOWDETAILS": "always"
])

test.environment([
        "SPRING_DATASOURCE_URL": testDbUrl,
        "WELCOME_MESSAGE": "Hello from test",
        "PORT": "8675",
        "MEMORY_LIMIT":"12G",
        "CF_INSTANCE_INDEX":"34",
        "CF_INSTANCE_ADDR": "123.sesame.street",
        "MANAGEMENT_ENDPOINTS_WEB_EXPOSURE_INCLUDE": "*",
        "MANAGEMENT_ENDPOINT_HEALTH_SHOWDETAILS": "always"
])

springBoot {
    buildInfo()
}